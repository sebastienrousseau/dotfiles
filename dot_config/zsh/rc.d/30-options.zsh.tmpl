# Zsh history and options
# Set secure default umask (077 = owner only, 022 = standard)
umask 022

HISTFILE="${HOME}/.zsh_history"
HISTSIZE=10000
SAVEHIST=10000
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_SAVE_NO_DUPS
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_FIND_NO_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_REDUCE_BLANKS
setopt EXTENDED_HISTORY
setopt SHARE_HISTORY
setopt APPEND_HISTORY

setopt AUTO_CD
setopt AUTO_PUSHD
setopt PUSHD_IGNORE_DUPS
setopt PUSHD_SILENT
setopt NO_BEEP

bindkey -v

# Fix: prevent auto-indent when pasting multi-line content (vi mode)
autoload -Uz bracketed-paste-magic
zle -N bracketed-paste bracketed-paste-magic

autoload -Uz up-line-or-beginning-search down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey -M viins "^[[A" up-line-or-beginning-search
bindkey -M viins "^[[B" down-line-or-beginning-search
bindkey -M viins "^P" up-line-or-beginning-search
bindkey -M viins "^N" down-line-or-beginning-search

ZSH_COMPLETIONS_DIR="${XDG_DATA_HOME:-${HOME}/.local/share}/zsh/completions"
if [[ ! -d "${ZSH_COMPLETIONS_DIR}" ]]; then
  command mkdir -p "${ZSH_COMPLETIONS_DIR}"
fi
if [[ -z ${fpath[(r)${ZSH_COMPLETIONS_DIR}]} ]]; then
  fpath=("${ZSH_COMPLETIONS_DIR}" "${fpath[@]}")
fi
if [[ -f "${HOME}/.local/bin/executable_dot_completion" ]]; then
  fpath=("${HOME}/.local/bin" "${fpath[@]}")
fi
# fnm (Fast Node Manager)
# Initialize early so Node + corepack use the expected version.
if command -v fnm >/dev/null; then
  if [[ ! -f "${ZSH_COMPLETIONS_DIR}/_fnm" ]]; then
    command fnm completions --shell zsh >| "${ZSH_COMPLETIONS_DIR}/_fnm"
  fi
  _lazy_load_fnm() {
    unfunction node npm npx pnpm pnpx fnm _lazy_load_fnm 2>/dev/null
    eval "$(command fnm env --use-on-cd --shell zsh --log-level quiet)"
  }
  fnm() { _lazy_load_fnm; command fnm "$@"; }
  node() { _lazy_load_fnm; command node "$@"; }
  npm() { _lazy_load_fnm; command npm "$@"; }
  npx() { _lazy_load_fnm; command npx "$@"; }
  pnpm() { _lazy_load_fnm; command pnpm "$@"; }
  pnpx() { _lazy_load_fnm; command pnpx "$@"; }
fi


# Lazy-load NVM (if fnm is not installed)
if ! command -v fnm >/dev/null; then
  export NVM_DIR="${NVM_DIR:-$HOME/.nvm}"
  if [[ -s "${NVM_DIR}/nvm.sh" ]]; then
    _lazy_load_nvm() {
      unfunction node npm npx nvm _lazy_load_nvm 2>/dev/null
      # shellcheck disable=SC1090
      . "${NVM_DIR}/nvm.sh"
      if [[ -s "${NVM_DIR}/bash_completion" ]]; then
        # shellcheck disable=SC1090
        . "${NVM_DIR}/bash_completion"
      fi
    }
    nvm() { _lazy_load_nvm; nvm "$@"; }
    node() { _lazy_load_nvm; command node "$@"; }
    npm() { _lazy_load_nvm; command npm "$@"; }
    npx() { _lazy_load_nvm; command npx "$@"; }
  fi
fi

# Lazy-load SDKMAN
export SDKMAN_DIR="${SDKMAN_DIR:-$HOME/.sdkman}"
if [[ -s "${SDKMAN_DIR}/bin/sdkman-init.sh" && -z "${functions[sdk]:-}" ]]; then
  _lazy_load_sdkman() {
    unfunction sdk _lazy_load_sdkman 2>/dev/null
    # shellcheck disable=SC1090
    . "${SDKMAN_DIR}/bin/sdkman-init.sh"
  }
  sdk() { _lazy_load_sdkman; sdk "$@"; }
fi
# Defer compinit to first prompt â€” saves ~37ms off startup
autoload -Uz compinit add-zsh-hook
_deferred_compinit() {
  add-zsh-hook -d precmd _deferred_compinit
  local _zinit_completions="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/completions"
  if [[ -d "$_zinit_completions" ]]; then
    local _c
    for _c in "$_zinit_completions"/_*; do
      [[ -L "$_c" && ! -e "$_c" ]] && rm -f "$_c"
    done
  fi
  local _compdump="${ZDOTDIR}/.zcompdump"
  if [[ -f "$_compdump" ]]; then
    local _missing=""
    local _line
    while IFS= read -r _line; do
      [[ "$_line" == "#files:"* ]] || continue
      _line="${_line#\#files: }"
      local -a _files
      _files=(${(z)_line})
      local _f
      for _f in "${_files[@]}"; do
        if [[ ! -e "$_f" ]]; then
          _missing=1
          break
        fi
      done
      [[ -n "$_missing" ]] && break
    done < "$_compdump"
    if [[ -n "$_missing" ]]; then
      rm -f "$_compdump" "${_compdump}.zwc"
    fi
  fi
  if [[ -n ${ZDOTDIR}/.zcompdump(#qN.mh+24) ]]; then
    compinit
  else
    compinit -C
  fi
  # Compile zcompdump to bytecode in background for faster subsequent loads
  { [[ ! -f "${ZDOTDIR}/.zcompdump.zwc" || "${ZDOTDIR}/.zcompdump" -nt "${ZDOTDIR}/.zcompdump.zwc" ]] && zcompile "${ZDOTDIR}/.zcompdump" } &!
}
add-zsh-hook precmd _deferred_compinit

zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*' special-dirs true
zstyle ':completion:*' verbose yes

autoload -Uz colors && colors
unsetopt correct_all

# direnv (directory environments)
if command -v direnv >/dev/null; then
  _cached_eval direnv-hook direnv hook zsh
fi
